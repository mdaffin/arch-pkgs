# Maintainer:  Michael Daffin <michael@daffin.io>

_pkgname=ale
_pkgauthor=w0rp
pkgname=neovim-ale
pkgver=2.2.0
pkgrel=1
pkgdesc="Asynchronous linting/fixing for Vim and Language Server Protocol (LSP) integration"
arch=('any')
url="https://github.com/${_pkgauthor}/${_pkgname}"
license=('MIT')
groups=('neovim-plugins')
depends=('neovim')
source=("https://github.com/${_pkgauthor}/${_pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('5a72557ed60803a3115516452bddff1f62485d8c90f711f3ee01a38ef3d3acf8')

package() {
  cd "${_pkgname}-${pkgver}"
  local installpath="$pkgdir/usr/share/nvim/runtime/pack/${_pkgauthor}/start/${_pkgname}"
  mkdir -p "$installpath"
  cp -a ale_linters autoload doc ftplugin plugin syntax "$installpath"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
