# Maintainer: Michael Daffin <michael@daffin.io>
pkgbase='mdaffin'
pkgname=(mdaffin-base mdaffin-desktop mdaffin-laptop mdaffin-dell-xps-13
mdaffin-devel mdaffin-devel-virtualisation mdaffin-devel-languages
mdaffin-devel-embedded mdaffin-arch-repo mdaffin-devel-work)
pkgver=14
pkgrel=1
pkgdesc="System config for mdaffin systems"
arch=(any)
url="https://github.com/mdaffin/arch-repo"
license=(MIT)
groups=(mdaffin)

source=(
    mdaffin-base.sh
    mdaffin-base.vim
    mdaffin-base.sudoers
    mdaffin-base-zsh.local
    mdaffin-base-vconsole.conf
    mdaffin-base-enp.network
    mdaffin-base-wlp.network
    mdaffin-desktop-sddm.conf
    mdaffin-desktop-00-keyboard.conf
    mdaffin-desktop-i3-config
    mdaffin-desktop.termite
    mdaffin-desktop.sh
    mdaffin-dell-xps-13-hidpi.sh
    mdaffin-dell-xps-13-hidpi-monitor.conf
    mdaffin-devel-rust.vim
    mdaffin-devel-rust.sh
    mdaffin-devel-go.vim
    mdaffin-devel-go.sh
    mdaffin-devel-web.sh
)

# The sources all live in this repo and so are version controlled and checked
# summed through git. There is not much point in adding these extra checksums
# here espically when they have to be changed for every update to them.
sha256sums=(SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP
SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP)

package_mdaffin-base() {
    provides=(vim vi)
    conflicts=(vim vi)
    install=mdaffin-base.install

    # Base group
    depends=(bash bzip2 coreutils cryptsetup device-mapper dhcpcd diffutils
    e2fsprogs file filesystem findutils gawk gcc-libs gettext glibc grep gzip
    inetutils iproute2 iputils less licenses linux logrotate man-db man-pages
    mdadm pacman pciutils pcmciautils perl procps-ng psmisc s-nail sed shadow
    sysfsutils systemd-sysvcompat tar texinfo usbutils util-linux which)
    # Explicitly removed from the base group: jfsutils lvm2 nano netctl reiserfsprogs vi xfsprogs

    # Extra general packages
    depends+=(sudo ripgrep exa fd tmux wget fzf unzip dialog)

    install -Dm 0644 mdaffin-base-vconsole.conf "${pkgdir}/etc/vconsole.conf"
    install -Dm 0640 mdaffin-base.sudoers "${pkgdir}/etc/sudoers.d/mdaffin-base"

    # Debugging tools
    depends+=(lsof bind-tools mtr socat htop iotop)

    # Networking
    depends+=(wpa_supplicant iwd avahi nss-mdns openssh)

    # Shells
    depends+=(zsh grml-zsh-config)

    install -Dm 0755 mdaffin-base.sh "${pkgdir}/etc/profile.d/mdaffin-base.sh"
    install -Dm 0644 mdaffin-base-zsh.local "${pkgdir}/etc/zsh/zshrc.local"

    # Filesystems
    depends+=(exfat-utils f2fs-tools)

    # Editor
    depends+=(oni neovim python-neovim python2-neovim neovim-languageclient
    nvim-completion-manager-git neovim-fugitive)
    install -dm 0755 "${pkgdir}/usr/bin"
    ln -s /usr/bin/nvim "${pkgdir}/usr/bin/vim"
    ln -s /usr/bin/nvim "${pkgdir}/usr/bin/vi"
    install -dm755 "${pkgdir}/usr/share/nvim/runtime/plugin/"
    ln -sf /usr/share/vim/vimfiles/plugin/fzf.vim "${pkgdir}/usr/share/nvim/runtime/plugin/fzf.vim"
    install -Dm 0644 mdaffin-base.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-base.vim"
    
    # Fonts
    depends+=(noto-fonts noto-fonts-cjk noto-fonts-emoji noto-fonts-extra
    ttf-monofur ttf-dejavu xorg-fonts-misc)

    install -Dm 0644 mdaffin-base-enp.network "${pkgdir}/usr/lib/systemd/network/90-mdaffin-base-enp.network"
    install -Dm 0644 mdaffin-base-wlp.network "${pkgdir}/usr/lib/systemd/network/90-mdaffin-base-wlp.network"
}

package_mdaffin-desktop() {
    install=mdaffin-desktop.install
    depends=(mdaffin-base)

    # I3 Desktop
    depends+=(rofi i3-wm i3status i3blocks i3lock dmenu)
    install -Dm 0644 mdaffin-desktop-i3-config "${pkgdir}/etc/i3/mdaffin-desktop-config"

    # Login manager
    depends+=(sddm)
    install -Dm 0644 mdaffin-desktop-sddm.conf "${pkgdir}/etc/sddm.conf"

    # Applications
    depends+=(termite firefox)
    install -Dm 0644 mdaffin-desktop.termite "${pkgdir}/etc/xdg/termite/mdaffin-desktop.config"

    # Not sure why these where installed :/ keeping for refence incase something breaks
    #openvpn
    #python-xdg

    # Utility
    depends+=(redshift bluez pulseaudio pulseaudio-alsa pavucontrol)

    # Drivers
    depends+=(libinput-gestures xf86-input-libinput xf86-input-synaptics
    xf86-video-intel xf86-video-vesa nvidia)

    # Xorg packages and utilities
    depends+=(xorg-xsetroot xorg-xkill xorg-xbacklight xorg-bdftopcf
    xorg-iceauth xorg-luit xorg-mkfontdir xorg-mkfontscale xorg-sessreg
    xorg-setxkbmap xorg-smproxy xorg-x11perf xorg-xauth xorg-xbacklight
    xorg-xcmsdb xorg-xcursorgen xorg-xdpyinfo xorg-xdriinfo xorg-xev
    xorg-xgamma xorg-xhost xorg-xinput xorg-xkbcomp xorg-xkbevd xorg-xkbutils
    xorg-xkill xorg-xlsatoms xorg-xlsclients xorg-xmodmap xorg-xpr xorg-xprop
    xorg-xrandr xorg-xrdb xorg-xrefresh xorg-xset xorg-xsetroot xorg-xvinfo
    xorg-xwd xorg-xwininfo xorg-xwud)

    install -Dm 0644 mdaffin-desktop-00-keyboard.conf "${pkgdir}/etc/X11/xorg.conf.d/00-keyboard.conf"
    install -Dm 0755 mdaffin-desktop.sh "${pkgdir}/etc/X11/xinit/xinitrc.d/30-mdaffin-desktop.sh"
}

package_mdaffin-laptop() {
    depends=('mdaffin-desktop' 'powertop')
}

package_mdaffin-dell-xps-13() {
    depends=('mdaffin-laptop')

    install -Dm 0755 mdaffin-dell-xps-13-hidpi.sh \
        "${pkgdir}/etc/X11/xinit/xinitrc.d/20-hidpi.sh"
    install -Dm 0644 mdaffin-dell-xps-13-hidpi-monitor.conf \
        "${pkgdir}/etc/X11/xorg.conf.d/90-hidpi-monitor.conf"
}

package_mdaffin-devel() {
    depends=(mdaffin-base)

    # Base Group
    depends+=(autoconf automake binutils bison fakeroot file findutils flex
    gawk gcc gettext grep groff gzip libtool m4 make pacman patch pkg-config
    sed sudo systemd texinfo util-linux which)

    # General tools
    depends+=(git cmake jq)
}

package_mdaffin-devel-virtualisation() {
    depends=(mdaffin-devel)

    # Virtualisation
    depends+=(virtualbox virtualbox-host-modules-arch vagrant)

    # Docker
    depends+=(docker docker-compose dnsmasq)
}

package_mdaffin-devel-languages() {
    depends=(mdaffin-devel)

    # Rust
    depends+=(neovim-rust-git rustup)
    install -Dm644 mdaffin-devel-rust.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-devel-rust.vim"
    install -Dm 0755 mdaffin-devel-rust.sh "${pkgdir}/etc/profile.d/mdaffin-devel-rust.sh"

    # Go
    depends+=(go dep neovim-go)
    install -Dm644 mdaffin-devel-go.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-devel-go.vim"
    install -Dm 0755 mdaffin-devel-go.sh "${pkgdir}/etc/profile.d/mdaffin-devel-go.sh"

    # Node
    depends+=(yarn nodejs neovim-posva-vim-vue-git)
    install -Dm 0755 mdaffin-devel-web.sh "${pkgdir}/etc/profile.d/mdaffin-devel-web.sh"
}

package_mdaffin-devel-embedded() {
    depends=(mdaffin-devel)

    depends+=(mdaffin-devel qemu qemu-arch-extra picocom arm-linux-gnueabihf-gcc ddrescue)
}

package_mdaffin-arch-repo() {
    depends=(mdaffin-devel)
    conflicts=(repose)

    depends+=(mdaffin-devel devtools aurutils s3cmd)
}

package_mdaffin-devel-work() {
    depends=(mdaffin-devel-virtualisation mdaffin-devel-languages terraform minikube)
}
